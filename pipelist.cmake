include_guard(GLOBAL)

# pipelist(<VERB> <pipelistVar> ...)
#
# Treat a pipe-separated "record" stored in a variable like a list for basic operations.
# NOTE: This assumes '|' does not appear inside fields. If it can, you need escaping/encoding.
#
# VERBS:
#   pipelist(GET     <pipelistVar> <index> <outVar> [DEFAULT <val>] [REQUIRED])
#   pipelist(LENGTH  <pipelistVar> <outVar>)
#   pipelist(SET     <pipelistVar> <index> <newValue> [EXTEND])      # modifies <pipelistVar> in-place
#   pipelist(REPLACE <pipelistVar> <index> <newValue> [EXTEND])      # alias of SET
function(pipelist verb pipelistVar)
    if(NOT verb)
        message(FATAL_ERROR "pipelist: missing verb (GET/LENGTH/SET/REPLACE)")
    else ()
        list(REMOVE_ITEM ARGV ${verb})
    endif()
    if(NOT pipelistVar)
        message(FATAL_ERROR "pipelist: missing pipelist variable name")
    else ()
        list(REMOVE_ITEM ARGV ${pipelistVar})
    endif()

    string(TOUPPER "${verb}" _P_VERB)

    if(_P_VERB STREQUAL "GET")
        set(options REQUIRED TOUPPER TOLOWER)
        set(oneValueArgs DEFAULT)
        set(multiValueArgs "")
        cmake_parse_arguments(_P "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGV})

        list(LENGTH _P_UNPARSED_ARGUMENTS _argc)
        if(NOT _argc EQUAL 2)
            message(FATAL_ERROR "pipelist(GET): expected <pipelistVar> <index> <outVar> [DEFAULT <val>] [REQUIRED] {TOUPPER} [TOLOWER]")
        endif()

        list(GET _P_UNPARSED_ARGUMENTS 0 _index)
        list(GET _P_UNPARSED_ARGUMENTS 1 _outVar)

        if(NOT _index MATCHES "^[0-9]+$")
            message(FATAL_ERROR "pipelist(GET): index must be a non-negative integer, got '${_index}'")
        endif()

        if(DEFINED ${pipelistVar})
            set(_record "${${pipelistVar}}")
        else()
            set(_record "")
        endif()

        string(REPLACE "|" ";" _tmp_list "${_record}")
        list(LENGTH _tmp_list _len)

        if(_index GREATER_EQUAL _len)
            if(_P_REQUIRED)
                message(FATAL_ERROR "pipelist(GET): index ${_index} out of range (len=${_len}) for ${pipelistVar}='${_record}'")
            endif()

            if(DEFINED _P_DEFAULT)
                set (_value "${_P_DEFAULT}")
                if (_P_TOUPPER)
                    string (TOUPPER "${_value}" _value)
                elseif (_P_TOLOWER)
                    string (TOLOWER "${_value}" _value)
                endif ()
                set(${_outVar} "${_value}" PARENT_SCOPE)
            else()
                unset(${_outVar})
            endif()
        else()
            list(GET _tmp_list ${_index} _value)
            if (_P_TOUPPER)
                string (TOUPPER "${_value}" _value)
            elseif (_P_TOLOWER)
                string (TOLOWER "${_value}" _value)
            endif ()
            set(${_outVar} "${_value}" PARENT_SCOPE)
        endif()
        unset(_record)
        unset(_tmp_list)
        unset(_len)
        unset(_value)
        unset(_index)
        unset(_outVar)
    elseif(_P_VERB STREQUAL "LENGTH")
        list(LENGTH ARGV _argc)
        if(NOT _argc EQUAL 1)
            message(FATAL_ERROR "pipelist(LENGTH): expected <pipelistVar> <outVar>")
        endif()

        list(GET ARGV 0 _outVar)

        if(DEFINED ${pipelistVar})
            set(_record "${${pipelistVar}}")
        else()
            set(_record "")
        endif()

        string(REPLACE "|" ";" _tmp_list "${_record}")
        list(LENGTH _tmp_list _len)
        set(${_outVar} "${_len}" PARENT_SCOPE)

        unset(_record)
        unset(_tmp_list)
        unset(_len)
        unset(_outVar)
    elseif(_P_VERB STREQUAL "SET" OR _P_VERB STREQUAL "REPLACE")
        set(options EXTEND)
        set(oneValueArgs "")
        set(multiValueArgs "")
        cmake_parse_arguments(_P "${options}" "${oneValueArgs}" "${multiValueArgs}" ${ARGV})

        list(LENGTH _P_UNPARSED_ARGUMENTS _argc)
        if(NOT _argc EQUAL 2)
            message(FATAL_ERROR "pipelist(${_P_VERB}): expected <pipelistVar> <index> <newValue> [EXTEND]")
        endif()

        list(GET _P_UNPARSED_ARGUMENTS 0 _index)
        list(GET _P_UNPARSED_ARGUMENTS 1 _newValue)

        if(NOT _index MATCHES "^[0-9]+$")
            message(FATAL_ERROR "pipelist(${_P_VERB}): index must be a non-negative integer, got '${_index}'")
        endif()

        if(DEFINED ${pipelistVar})
            set(_record "${${pipelistVar}}")
        else()
            set(_record "")
        endif()

        string(REPLACE "|" ";" _tmp_list "${_record}")
        list(LENGTH _tmp_list _len)

        if(_index GREATER_EQUAL _len)
            if(_P_EXTEND)
                # Pad with empty fields up to index, then append value
                while(_len LESS _index)
                    list(APPEND _tmp_list "")
                    list(LENGTH _tmp_list _len)
                endwhile()
                list(APPEND _tmp_list "${_newValue}")
            else()
                message(FATAL_ERROR "pipelist(${_P_VERB}): index ${_index} out of range (len=${_len}) for ${pipelistVar}='${_record}'. Use EXTEND to grow.")
            endif()
        else()
            list(REMOVE_AT _tmp_list ${_index})
            list(INSERT _tmp_list ${_index} "${_newValue}")
        endif()

        string(REPLACE ";" "|" _joined "${_tmp_list}")
        set(${pipelistVar} "${_joined}" PARENT_SCOPE)

        unset(_record)
        unset(_tmp_list)
        unset(_len)
        unset(_joined)
        unset(_index)
        unset(_newValue)
    elseif(_P_VERB STREQUAL "POP_FRONT" OR _P_VERB STREQUAL "POP_BACK")
        list(LENGTH ARGV _argc)
        if(NOT _argc EQUAL 1)
            message(FATAL_ERROR "pipelist(${_P_VERB}): expected <pipelistVar> <outVar>")
        endif()

        list(GET ARGV 0 _outVar)

        if(DEFINED ${pipelistVar})
            set(_record "${${pipelistVar}}")
        else()
            set(_record "")
        endif()

        string(REPLACE "|" ";" _tmp_list "${_record}")

        if("${_P_VERB}" STREQUAL "POP_FRONT")
            list(POP_FRONT _tmp_list _value)
        else ()
            list(POP_BACK  _tmp_list _value)
        endif ()
        set(${_outVar} "${_value}" PARENT_SCOPE)

        string(REPLACE ";" "|" _record "${_tmp_list}")
        set(${pipelistVar} "${_record}" PARENT_SCOPE)

        unset(_record)
        unset(_tmp_list)
        unset(_len)
        unset(_outVar)
        unset(_value)
    else()
        message(FATAL_ERROR "pipelist: unknown verb '${verb}' (GET/LENGTH/SET/REPLACE)")
    endif()

    unset(_P_VERB)
endfunction()