
function(soci_process incs libs defs)

    set(soci_IncludePathsList ${${incs}})
    set(soci_LibrariesList ${${libs}})
    set(soci_DefinesList ${${defs}})

    find_package(SQLite3)
    if (NOT SQLite3_FOUND)
        set(fail ON PARENT_SCOPE)
        message(WARNING "SQLite3 not installed or not found by CMake")
        return()
    endif ()

    # Prefer modern package variables, with robust fallbacks for Homebrew
    set(_sqlite_inc)
    if(DEFINED SQLite3_INCLUDE_DIRS AND NOT SQLite3_INCLUDE_DIRS STREQUAL "")
        set(_sqlite_inc "${SQLite3_INCLUDE_DIRS}")
    elseif(DEFINED SQLite3_INCLUDE_DIR AND NOT SQLite3_INCLUDE_DIR STREQUAL "")
        set(_sqlite_inc "${SQLite3_INCLUDE_DIR}")
    elseif(APPLE AND EXISTS "/opt/homebrew/include/sqlite3.h")
        # Homebrew default prefix symlink (stable across versions)
        set(_sqlite_inc "/opt/homebrew/include")
    endif()
    if(_sqlite_inc)
        list(APPEND soci_IncludePathsList "${_sqlite_inc}")
    endif()

    if(TARGET SQLite::SQLite3)
        list(APPEND soci_LibrariesList SQLite::SQLite3)
    elseif(DEFINED SQLite3_LIBRARIES AND NOT SQLite3_LIBRARIES STREQUAL "")
        list(APPEND soci_LibrariesList ${SQLite3_LIBRARIES})
    elseif(DEFINED SQLite3_LIBRARY AND NOT SQLite3_LIBRARY STREQUAL "")
        list(APPEND soci_LibrariesList ${SQLite3_LIBRARY})
    elseif(APPLE AND EXISTS "/opt/homebrew/lib/libsqlite3.dylib")
        list(APPEND soci_LibrariesList "/opt/homebrew/lib/libsqlite3.dylib")
    else()
        message(WARNING "SQLite3 library not found; you may need to install sqlite via Homebrew or set SQLite3_ROOT/CMAKE_PREFIX_PATH")
    endif()

    if (APPLE)
        find_package(SOCI)
        if (NOT SOCI_FOUND)
            set(fail ON PARENT_SCOPE)
            message(WARNING "SOCI not installed")
            return()
        endif ()
    elseif(LINUX)
        find_package(SOCI)
        if (NOT SOCI_Core_FOUND OR NOT SOCI_SQLite3_FOUND)
            set(fail ON PARENT_SCOPE)
            message(WARNING "SOCI not installed")
            return()
        endif ()
    endif ()
    list(APPEND soci_LibrariesList SOCI::soci)
    list(APPEND soci_DefinesList USING_DATABASE USING_soci)

    addTarget(SOCI::soci soci OFF "")

    set(_IncludePathsList ${soci_IncludePathsList} PARENT_SCOPE)
    set(_LibrariesList ${soci_LibrariesList} PARENT_SCOPE)
    set(_DefinesList ${soci_DefinesList} PARENT_SCOPE)

    set(fail OFF PARENT_SCOPE)
    set(HANDLED ON PARENT_SCOPE)

endfunction()

soci_process(_IncludePathsList _LibrariesList _DefinesList)
