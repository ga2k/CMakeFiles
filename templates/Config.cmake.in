# CMake package configuration (installed)
@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

# We expose an imported target HoffSoft::<lib>> and keep third-party deps internal.
# This lets consumers just link HoffSoft::<lib> without needing to locate deps at configure time.

if(NOT TARGET @APP_VENDOR@::@APP_NAME@)
  add_library(@APP_VENDOR@::@APP_NAME@ SHARED IMPORTED)
endif()

# Resolve installed library path (cover common layouts)
set(_hs_prefix "${PACKAGE_PREFIX_DIR}")
set(_hs_common_lib "@APP_VENDOR_LC@_@APP_NAME_LC@")
set(_hs_multi_lib "@APP_VENDOR_LC@_@APP_NAME_LC@@MULTI_LIB_DECLARATOR@")

set(_candidates
  "${_hs_prefix}/lib/lib${_hs_common_lib}.so"
  "${_hs_prefix}/lib64/lib${_hs_common_lib}.so"
  "${_hs_prefix}/lib/lib${_hs_common_lib}.dynlib"
  "${_hs_prefix}/bin/${_hs_common_lib}.dll"
  "${_hs_prefix}/lib/${_hs_common_lib}.dll"
)

set(_hs_lib "")

if (NOT "${_hs_common_lib}" STREQUAL "${_hs_multi_lib}")
  set(_extended_candidates
    "${_hs_prefix}/lib/lib${_hs_multi_lib}.so"
    "${_hs_prefix}/lib64/lib${_hs_multi_lib}.so"
  )
  foreach(_c ${_extended_candidates})
    if(EXISTS "${_c}")
      set(_hs_lib "${_c}")
      set(_current_candidate_lib_if_any "/@CURRENT_GFX_LIB@")
      break()
    endif()
  endforeach()
else ()
  foreach(_c ${_candidates})
    if(EXISTS "${_c}")
      set(_hs_lib "${_c}")
      set(_current_candidate_lib_if_any "")
      break()
    endif()
  endforeach()
endif ()

if(NOT _hs_lib)
  message(FATAL_ERROR "@APP_VENDOR@::@APP_NAME@ library not found under ${_hs_prefix}")
endif()

set_target_properties(@APP_VENDOR@::@APP_NAME@ PROPERTIES
  IMPORTED_LOCATION "${_hs_lib}"
  INTERFACE_INCLUDE_DIRECTORIES "${_hs_prefix}/include"
)

# For Clang consumers of modules, provide prebuilt-module path automatically
if(CMAKE_CXX_COMPILER_ID MATCHES "Clang")
  target_compile_options(@APP_VENDOR@::@APP_NAME@ INTERFACE
    "-fprebuilt-module-path=${_hs_prefix}/share/cmake/cxx/pcm/@APP_VENDOR@${_current_candidate_lib_if_any}/@APP_NAME@.dir"
  )
endif()

# Convenience variables
set(@_TARGET@_INCLUDE_DIR  "${_hs_prefix}/include")
set(@_TARGET@_INCLUDE_DIRS "${_hs_prefix}/include")
set(@_TARGET@_LIBRARIES    @APP_VENDOR@::@APP_NAME@)
