# CMake package configuration (installed)
@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

# Provide a wxWidgets shim for consumers.
# GfxTarget.cmake exports HoffSoft::wxWidgets that links to wx::wxmono, but consumers may not have
# wxWidgets' own CMake package (and thus no wx::wxmono target). We stage the real binaries, so we
# create an imported target that points at them *when they exist*.
#
# IMPORTANT: In superbuild flows, find_package(Gfx) may happen before staging has copied wx binaries
# into the prefix. In that case, we must NOT fail at configure time.
if(WIN32 AND NOT TARGET wx::wxmono)
  set(_wx_bin_dir "${PACKAGE_PREFIX_DIR}/${CMAKE_INSTALL_BINDIR}")
  set(_wx_lib_dir "${PACKAGE_PREFIX_DIR}/${CMAKE_INSTALL_LIBDIR}")

  file(GLOB _wx_dll_candidates
      "${_wx_bin_dir}/wxmsw*u*.dll"
      "${_wx_bin_dir}/wxmsw*.dll"
  )
  list(REMOVE_DUPLICATES _wx_dll_candidates)

  file(GLOB _wx_implib_candidates
      "${_wx_lib_dir}/wx_mswu-*.lib"
      "${_wx_lib_dir}/wxmswu*.lib"
  )
  list(REMOVE_DUPLICATES _wx_implib_candidates)

  list(LENGTH _wx_dll_candidates _wx_dll_count)
  list(LENGTH _wx_implib_candidates _wx_implib_count)

  if(_wx_dll_count EQUAL 1 AND _wx_implib_count EQUAL 1)
    list(GET _wx_dll_candidates 0 _wx_mono_dll)
    list(GET _wx_implib_candidates 0 _wx_mono_implib)

    add_library(wx::wxmono SHARED IMPORTED)
    set_target_properties(wx::wxmono PROPERTIES
        IMPORTED_LOCATION "${_wx_mono_dll}"
        IMPORTED_IMPLIB   "${_wx_mono_implib}"
        INTERFACE_INCLUDE_DIRECTORIES "${PACKAGE_PREFIX_DIR}/include"
    )
  else()
    # Not staged yet (or ambiguous). Don't fail configure; just leave it unresolved for now.
    # If a consumer truly needs wx::wxmono and nothing provides it, they'll fail later with a clear error.
    message(STATUS
        "Gfx package: wxWidgets shim not created (yet).\n"
        "  bin: ${_wx_bin_dir}\n"
        "  dll candidates: ${_wx_dll_candidates}\n"
        "  lib: ${_wx_lib_dir}\n"
        "  implib candidates: ${_wx_implib_candidates}")
  endif()

  unset(_wx_bin_dir)
  unset(_wx_lib_dir)
  unset(_wx_dll_candidates)
  unset(_wx_implib_candidates)
  unset(_wx_dll_count)
  unset(_wx_implib_count)
  unset(_wx_mono_dll)
  unset(_wx_mono_implib)
endif()

if ("${CMAKE_CURRENT_LIST_DIR}" STREQUAL "@OUTPUT_DIR@")
  message(NOTICE "In development")
  set(PACKAGE_PREFIX_DIR "${CMAKE_CURRENT_LIST_DIR}")

  set(_inDevelopment  ON)
  set(_inStaging      OFF)
  set(_installed      OFF)

  list(APPEND _searchDirs
  "${PACKAGE_PREFIX_DIR}/${CMAKE_INSTALL_LIBDIR}"
  "${PACKAGE_PREFIX_DIR}/${CMAKE_INSTALL_BINDIR}"
  "${PACKAGE_PREFIX_DIR}/${CMAKE_INSTALL_LIBDIR}/${BUILD_TYPE}"
  "${PACKAGE_PREFIX_DIR}/${CMAKE_INSTALL_BINDIR}/${BUILD_TYPE}"
  )

  # Search upwards for the "include" directory
  set(_searchDir "${PACKAGE_PREFIX_DIR}")
  set(_stopDir "@APP_NAME@")
  set(_foundInc OFF)

  while(NOT _foundInc)
    if(EXISTS "${_searchDir}/include")
      set(_incdir "${_searchDir}/include")
      set(_foundInc ON)
      break()
    else()
      # Fail if we go higher than the parent of HoffSoft or hit root
      if("${_justDir}" STREQUAL "${_stopDir}")
        message(FATAL_ERROR "Could not find 'include' directory within development tree limits.")
      endif()

      get_filename_component(_parentDir "${_searchDir}/.." ABSOLUTE)
      get_filename_component(_justDir "${_parentDir}" NAME)

      set(_searchDir "${_parentDir}")
    endif()
  endwhile()

elseif ("${PACKAGE_PREFIX_DIR}" STREQUAL "@STAGED_PATH@")
  message(NOTICE "Staged")

  set(_inDevelopment  OFF)
  set(_inStaging      ON)
  set(_installed      OFF)

  set(_incdir "${PACKAGE_PREFIX_DIR}/include")

  list(APPEND _searchDirs
    "${PACKAGE_PREFIX_DIR}/${CMAKE_INSTALL_LIBDIR}"
    "${PACKAGE_PREFIX_DIR}/${CMAKE_INSTALL_BINDIR}"
    "${PACKAGE_PREFIX_DIR}/${CMAKE_INSTALL_LIBDIR}/${BUILD_TYPE}"
    "${PACKAGE_PREFIX_DIR}/${CMAKE_INSTALL_BINDIR}/${BUILD_TYPE}"
  )

elseif ("${PACKAGE_PREFIX_DIR}" STREQUAL "@SYSTEM_PATH@")
  message(NOTICE "Installed")

  set(_inDevelopment  OFF)
  set(_inStaging      OFF)
  set(_installed      ON)
  
  set(_incdir "${PACKAGE_PREFIX_DIR}/include")
  
  list(APPEND _searchDirs
    "${PACKAGE_PREFIX_DIR}/${CMAKE_INSTALL_LIBDIR}"
    "${PACKAGE_PREFIX_DIR}/${CMAKE_INSTALL_BINDIR}"
    "${PACKAGE_PREFIX_DIR}/${CMAKE_INSTALL_LIBDIR}/${BUILD_TYPE}"
    "${PACKAGE_PREFIX_DIR}/${CMAKE_INSTALL_BINDIR}/${BUILD_TYPE}"
  )
else ()
  message(FATAL_ERROR "Illegal installation")
endif ()

set(_actualLibName "@CMAKE_SHARED_LIBRARY_PREFIX@@APP_VENDOR_LC@_@APP_NAME_LC@@CMAKE_SHARED_LIBRARY_SUFFIX@")
set(_winImpLibName "@CMAKE_SHARED_LIBRARY_PREFIX@@APP_VENDOR_LC@_@APP_NAME_LC@.lib")

foreach(_dir IN LISTS _searchDirs)
  if(EXISTS "${_dir}/${_actualLibName}")
    set(_lib    "${_dir}/${_actualLibName}")
    set(_libdir "${_dir}")
    message(AUTHOR_WARNING "LIB is ${_lib}")
  endif()
  if(WIN32 AND EXISTS "${_dir}/${_winImpLibName}")
    set(_imp    "${_dir}/${_winImpLibName}")
    set(_impdir "${_dir}")
    message(AUTHOR_WARNING "IMP is ${_imp}")
  endif()
endforeach()

if(NOT _lib)
  message(NOTICE "@APP_VENDOR@::@APP_NAME@ (${_actualLibName}) not found using")
  foreach(_dir IN LISTS _searchDirs)
    message(NOTICE "${_dir}/${_actualLibName}")
  endforeach()
  message(FATAL_ERROR "Halted")
endif()

if(WIN32)
  if(NOT _imp)
    message(NOTICE "@APP_VENDOR@::@APP_NAME@ (${_winImpLibName}) not found using")
    foreach(_dir IN LISTS _searchDirs)
      message(NOTICE "${_dir}/${_winImpLibName}")
    endforeach()
    message(FATAL_ERROR "Halted")
  endif()
  set(_libdir "${_impdir}")
endif()
  
if(_inDevelopment)
  # In development (monorepo) include the build-tree export of module FILE_SET mappings
  file(GLOB _DEV_MODULE_EXPORT
       "@BUILD_DIR@/CMakeFiles/Export/*/cxx/@APP_VENDOR@/@APP_NAME@/cxx-modules-@APP_NAME@Target.cmake")
  if(_DEV_MODULE_EXPORT)
    list(GET _DEV_MODULE_EXPORT 0 _DEV_MODULE_EXPORT_FILE)
    include("${_DEV_MODULE_EXPORT_FILE}")
  endif()
elseif(EXISTS "${_libdir}/cmake/@APP_NAME@Target.cmake")
  include("${_libdir}/cmake/@APP_NAME@Target.cmake")
elseif(EXISTS "${_libdir}/cmake/cxx/@APP_VENDOR@/@APP_NAME@/cxx-modules-@APP_NAME@Target.cmake")
  include("${_libdir}/cmake/cxx/@APP_VENDOR@/@APP_NAME@/cxx-modules-@APP_NAME@Target.cmake")
endif()

if(WIN32)
  # Add the wx/setup.h header to the include path
  if (TARGET @APP_VENDOR@::wxWidgets)
    set_property(TARGET @APP_VENDOR@::wxWidgets
        APPEND PROPERTY INTERFACE_INCLUDE_DIRECTORIES
            "${_incdir}/msvc"
    )
  endif()
  set_target_properties(@APP_VENDOR@::@APP_NAME@ PROPERTIES IMPORTED_IMPLIB "${_impdir}/${_winImpLibName}")
  set(_libdir "${_impdir}")
endif()

# For Clang consumers of modules, optionally provide prebuilt-module path (PCMs/BMIs).
# This can be disabled by setting USE_PREBUILT_MODULES=OFF in the consuming configure preset.
if(NOT DEFINED USE_PREBUILT_MODULES)
  # Default to ON to preserve existing behavior unless explicitly overridden.
  set(USE_PREBUILT_MODULES ON)
endif()

if(USE_PREBUILT_MODULES)
  if(CMAKE_CXX_COMPILER_ID MATCHES "Clang|GCC")
    if(CMAKE_CXX_COMPILER_ID MATCHES "Clang")
      set(fmodule_cmd "-fprebuilt-module-path")
    else()
      set(fmodule_cmd "-fmodule-mapper")
    endif()

    if(_inDevelopment)
      target_compile_options(@APP_VENDOR@::@APP_NAME@ INTERFACE
        "${fmodule_cmd}=@BUILD_DIR@/src/CMakeFiles/@APP_NAME@.dir")
    else()
      target_compile_options(@APP_VENDOR@::@APP_NAME@ INTERFACE
        "${fmodule_cmd}=${libdir}/cmake/bmi/@APP_VENDOR@/@APP_NAME@")
    endif()
  elseif(MSVC)
    if(_inDevelopment)
      set(_bmiDir "@BUILD_DIR@/src/@APP_NAME@.dir/@BUILD_TYPE@")
    else()
      set(_bmiDir "${_libdir}/cmake/bmi/@APP_VENDOR@/@APP_NAME@/@BUILD_TYPE@")
    endif()
  
    # IMPORTANT: Strip the BMI-only source mapping that crashes the VS generator.
    # This forces VS to rely solely on the /ifcSearchDir provided below.
    set_target_properties(@APP_VENDOR@::@APP_NAME@ PROPERTIES
        IMPORTED_CXX_MODULES_DEBUG ""
        IMPORTED_CXX_MODULES_RELEASE "")

    set_property(TARGET @APP_VENDOR@::@APP_NAME@ APPEND PROPERTY
        INTERFACE_COMPILE_OPTIONS "/ifcSearchDir:${_bmiDir}")

  endif()
endif()

# Convenience variables
set(@APP_NAME@_INCLUDE_DIR  "${_incdir}" )
set(@APP_NAME@_INCLUDE_DIR  "${@APP_NAME@_INCLUDE_DIR}" PARENT_SCOPE)
set(@APP_NAME@_LIBRARIES    @APP_VENDOR@::@APP_NAME@ )
set(@APP_NAME@_LIBRARIES    "${@APP_NAME@_LIBRARIES}"   PARENT_SCOPE)
set(@APP_NAME@_LIBRARY_DIR  "${_libdir}" )
set(@APP_NAME@_LIBRARY_DIR  "${@APP_NAME@_LIBRARY_DIR}" PARENT_SCOPE)

# Plugin and resource directories
if (NOT "@APP_CREATES_PLUGINS@" STREQUAL "")
  # If the app creates plugins, set the plugin directory
  if (_inDevelopment)
    set(PLUGIN_DIR "@OUTPUT_DIR@/plugins")
  else ()
    set(PLUGIN_DIR "${_libdir}/@APP_VENDOR@/@APP_NAME@/plugins")
  endif ()
  set(PLUGIN_DIR              "${PLUGIN_DIR}" PARENT_SCOPE)
  set(@APP_VENDOR@_PLUGIN_DIR "${PLUGIN_DIR}" PARENT_SCOPE)
endif()

if (NOT "@APP_SUPPLIES_RESOURCES@" STREQUAL "")
  # If the app supplies resources, set the resource directory
  if (_inDevelopment)
    set(RESOURCE_DIR "@CMAKE_SOURCE_DIR@/resources")
  else ()
    set(RESOURCE_DIR "${PACKAGE_PREFIX_DIR}/share/@APP_VENDOR@/@APP_NAME@/resources")
  endif ()
  set(@APP_NAME@_RESOURCE_DIR "${RESOURCE_DIR}" PARENT_SCOPE)
  set(RESOURCE_DIR "${RESOURCE_DIR}" PARENT_SCOPE)
elseif (NOT "@APP_INCLUDES_RESOURCES@" STREQUAL "")
  # If the app consumes resources, set the resource directory
  if (_inDevelopment)
    set(RESOURCE_DIR "@CMAKE_SOURCE_DIR@/resources")
  else ()
    set(RESOURCE_DIR "${PACKAGE_PREFIX_DIR}/share/@APP_VENDOR@/@APP_NAME@/resources")
  endif ()
set(@APP_NAME@_RESOURCE_DIR "${RESOURCE_DIR}" PARENT_SCOPE)
set(RESOURCE_DIR "${RESOURCE_DIR}" PARENT_SCOPE)
endif()

unset(_inDevelopment)
unset(_incdir)
unset(_libdir)
unset(_lib)
unset(_dir)
unset(_DEV_MODULE_EXPORT)
unset(_DEV_MODULE_EXPORT_FILE)
unset(_searchDirs)
unset(_actualLibName)
