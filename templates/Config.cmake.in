# CMake package configuration (installed)
@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

# --- Transitive dependencies ---------------------------------------------------
# If this package exposes headers/APIs that rely on third‑party libraries,
# declare them here so downstream `find_package(@APP_VENDOR@::@APP_NAME@)`
# pulls in the same instance instead of each consumer fetching its own copy.
#
# Reflection (magic_enum) — header‑only. Must be UNIQUE across module TUs.
# Signal (eventpp) — header‑only.
# Testing (GTest) — linkable targets.
# YAML (yaml-cpp) — linkable target.
# We keep them QUIET to allow consumers that don’t need them; when required,
# consumers must ensure resolvable via CMAKE_PREFIX_PATH.
find_dependency(magic_enum QUIET)
find_dependency(eventpp    QUIET)
find_dependency(GTest      QUIET)
find_dependency(yaml-cpp   QUIET)

message(STATUS "@APP_VENDOR@::@APP_NAME@  PROJECT_SOURCE_DIR=@PROJECT_SOURCE_DIR@ ")

# Resolve installed library path (cover common layouts)
set(_libdir "${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@")
set(_incdir "${PACKAGE_PREFIX_DIR}/include")
set(_lib "${_libdir}/@CMAKE_SHARED_LIBRARY_PREFIX@@APP_VENDOR_LC@_@APP_NAME_LC@@CMAKE_SHARED_LIBRARY_SUFFIX@")
if(EXISTS "${_lib}")
  set(InDevelopment OFF)
else()
  set(_libdir "@OUTPUT_DIR@/dll")
  if (@SUPERBUILD@)
    set(_incdir "@CMAKE_SOURCE_DIR@/@APP_NAME@/include")
  else()
    set(_incdir "@CMAKE_SOURCE_DIR@/include")
  endif()
  set(_lib "${_libdir}/@CMAKE_SHARED_LIBRARY_PREFIX@@APP_VENDOR_LC@_@APP_NAME_LC@@CMAKE_SHARED_LIBRARY_SUFFIX@")
  if(EXISTS "${_lib}")
    set(InDevelopment ON)
  else()
    message(FATAL_ERROR "@APP_VENDOR@::@APP_NAME@ library not found under ${PACKAGE_PREFIX_DIR} or @OUTPUT_DIR@")
  endif()
endif()

if(NOT TARGET @APP_VENDOR@::@APP_NAME@)
  add_library(@APP_VENDOR@::@APP_NAME@ SHARED IMPORTED)
  set_target_properties(@APP_VENDOR@::@APP_NAME@ PROPERTIES
    IMPORTED_LOCATION "${_lib}"
    CXX_STANDARD 23
    INTERFACE_INCLUDE_DIRECTORIES "${_incdir}"
  )
endif()
if(NOT InDevelopment)
  include("${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@/cmake/cxx/@APP_VENDOR@/cxx-modules-@APP_NAME@Target.cmake")
endif()

# For Clang consumers of modules, provide prebuilt-module path automatically
if(CMAKE_CXX_COMPILER_ID MATCHES "Clang|GCC")
  if(CMAKE_CXX_COMPILER_ID MATCHES "Clang")
    set(fmodule_cmd "-fprebuilt-module-path")
  else()
    set(fmodule_cmd "-fmodule-mapper")
  endif()

  if(InDevelopment)
    target_compile_options(@APP_VENDOR@::@APP_NAME@ INTERFACE
      "${fmodule_cmd}=@BUILD_DIR@/src/CMakeFiles/@APP_NAME@.dir")
  else()
    target_compile_options(@APP_VENDOR@::@APP_NAME@ INTERFACE
      "${fmodule_cmd}=${PACKAGE_PREFIX_DIR}/@CMAKE_INSTALL_LIBDIR@/cmake/bmi/@APP_VENDOR@/@APP_NAME@")
  endif()
endif()

# Link transitive dependencies when available so consumers don’t create duplicates.
if(TARGET magic_enum::magic_enum)
  set_property(TARGET @APP_VENDOR@::@APP_NAME@ APPEND PROPERTY
    INTERFACE_LINK_LIBRARIES magic_enum::magic_enum)
endif()
if(TARGET eventpp::eventpp)
  set_property(TARGET @APP_VENDOR@::@APP_NAME@ APPEND PROPERTY
    INTERFACE_LINK_LIBRARIES eventpp::eventpp)
endif()
if(TARGET GTest::gtest)
  set_property(TARGET @APP_VENDOR@::@APP_NAME@ APPEND PROPERTY
    INTERFACE_LINK_LIBRARIES GTest::gtest)
endif()
if(TARGET yaml-cpp)
  set_property(TARGET @APP_VENDOR@::@APP_NAME@ APPEND PROPERTY
    INTERFACE_LINK_LIBRARIES yaml-cpp)
endif()

# Convenience variables
set(@APP_NAME@_INCLUDE_DIR  "${_incdir}" PARENT_SCOPE)
set(@APP_NAME@_LIBRARIES    @APP_VENDOR@::@APP_NAME@ PARENT_SCOPE)
set(@APP_NAME@_LIBRARY_DIR  "${_libdir}" PARENT_SCOPE)

# Plugin and resource directories

if (NOT "@APP_CREATES_PLUGINS@" STREQUAL "")
  # If the app creates plugins, set the plugin directory
  if (InDevelopment)
    set(PLUGIN_DIR "@OUTPUT_DIR@/plugins")
  else ()
    set(PLUGIN_DIR "${PACKAGE_PREFIX_DIR}/lib64/@APP_VENDOR@/@APP_NAME@/plugins")
  endif ()
  set(@APP_VENDOR@_PLUGIN_DIR "${PLUGIN_DIR}" PARENT_SCOPE)
endif()

if (NOT "@APP_SUPPLIES_RESOURCES@" STREQUAL "")
  # If the app supplies resources, set the resource directory
  if (InDevelopment)
    set(RESOURCE_DIR "@CMAKE_SOURCE_DIR@/resources")
  else ()
    set(RESOURCE_DIR "${PACKAGE_PREFIX_DIR}/share/@APP_VENDOR@/@APP_NAME@/resources")
  endif ()
  set(@APP_NAME@_RESOURCE_DIR "${RESOURCE_DIR}" PARENT_SCOPE)
elseif (NOT "@APP_INCLUDES_RESOURCES@" STREQUAL "")
  # If the app consumes resources, set the resource directory
  if (InDevelopment)
    set(RESOURCE_DIR "@CMAKE_SOURCE_DIR@/resources")
  else ()
    set(RESOURCE_DIR "${PACKAGE_PREFIX_DIR}/share/@APP_VENDOR@/@APP_NAME@/resources")
  endif ()
set(@APP_NAME@_RESOURCE_DIR "${RESOURCE_DIR}" PARENT_SCOPE)
endif()
